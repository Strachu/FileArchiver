; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppName=FileArchiver
VersionInfoProductName=FileArchiver
AppPublisher=Patryk Strach
VersionInfoCompany=Patryk Strach
VersionInfoCopyright=Patryk Strach
AppVersion=0.1.0
VersionInfoVersion=0.1.0
AppId={{5EFF25B8-5075-4A8F-8804-779363C2DFB1}
SourceDir=..\
OutputDir=Installers\
OutputBaseFilename=FileArchiver Setup
ArchitecturesInstallIn64BitMode=x64 ia64
DefaultDirName={pf}\FileArchiver
DefaultGroupName=FileArchiver
AllowNoIcons=yes
Compression=lzma/ultra
SolidCompression=yes
ChangesAssociations=True
UninstallDisplayName=FileArchiver
ShowTasksTreeLines=True
AlwaysShowDirOnReadyPage=True
AllowRootDirectory=True

[Files]
Source: "Binaries\Release\FileArchiver.Core.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace uninsrestartdelete
Source: "Binaries\Release\FileArchiver.exe"; DestDir: "{app}"; Flags: ignoreversion restartreplace uninsrestartdelete
Source: "Binaries\Release\System.IO.Abstractions.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace uninsrestartdelete;
Source: "Binaries\Release\pl-PL\*"; DestDir: "{app}\pl-PL"; Flags: ignoreversion restartreplace uninsrestartdelete

Source: "Binaries\Release\FileArchiver.ContextMenu.exe"; DestDir: "{app}"; Flags: ignoreversion restartreplace uninsrestartdelete; Components: ContextMenuIntegration
Source: "Binaries\Release\Liferay.Nativity.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace uninsrestartdelete; Components: ContextMenuIntegration
Source: "Binaries\Release\LiferayNativityContextMenus_x64.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace uninsrestartdelete 64bit regserver; Components: ContextMenuIntegration; Check: Is64BitInstallMode
Source: "Binaries\Release\LiferayNativityContextMenus_x86.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace uninsrestartdelete 32bit regserver; Components: ContextMenuIntegration; Check: not Is64BitInstallMode
Source: "Binaries\Release\LiferayNativityUtil_x64.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace uninsrestartdelete; Components: ContextMenuIntegration; Check: Is64BitInstallMode
Source: "Binaries\Release\LiferayNativityUtil_x86.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace uninsrestartdelete; Components: ContextMenuIntegration; Check: not Is64BitInstallMode
Source: "Binaries\Release\log4net.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace uninsrestartdelete; Components: ContextMenuIntegration
Source: "Binaries\Release\Newtonsoft.Json.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace uninsrestartdelete; Components: ContextMenuIntegration

Source: "Binaries\Release\Plugins\*"; Excludes: "*.pdb,FileArchiver.Core.*,FileArchiver.exe,FileArchiver.pdb"; DestDir: "{app}\Plugins"; Flags: ignoreversion restartreplace uninsrestartdelete recursesubdirs

[Components]
Name: "ContextMenuIntegration"; Description: "{cm:ContextMenuIntegrationComponent}"; Types: full custom compact

[CustomMessages]
english.ContextMenuIntegrationComponent=Integration with the context menu of File Explorer
polish.ContextMenuIntegrationComponent=Integracja z menu kontekstowym eksplorera
english.CreateFileAssocations=Associate the application with supported files
polish.CreateFileAssocations=Skojarz aplikacjê z obs³ugiwanymi plikami

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "polish"; MessagesFile: "compiler:Languages\Polish.isl"

[Tasks]
Name: "DesktopIcon"; Description: "{cm:CreateDesktopIcon}"; Flags: unchecked
Name: "FileAssocations"; Description: "{cm:CreateFileAssocations}"

[Icons]
Name: "{commondesktop}\FileArchiver"; Filename: "{app}\FileArchiver.exe"; Tasks: desktopicon
Name: "{group}\FileArchiver"; Filename: "{app}\FileArchiver.exe"
Name: "{group}\{cm:UninstallProgram, FileArchiver}"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\FileArchiver.exe"; Flags: nowait postinstall skipifsilent unchecked; Description: "{cm:LaunchProgram,FileArchiver}"
Filename: "{app}\FileArchiver.ContextMenu.exe"; Flags: nowait runasoriginaluser; Components: ContextMenuIntegration

[Registry]
Root: "HKLM"; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "FileArchiverContextMenu"; ValueData: "{app}\FileArchiver.ContextMenu.exe"; Flags: uninsdeletevalue; Components: ContextMenuIntegration
Root: "HKCR"; Subkey: ".7z"; ValueType: string; ValueData: "FileArchiver"; Flags: uninsdeletevalue; Tasks: FileAssocations
Root: "HKCR"; Subkey: ".zip"; ValueType: string; ValueData: "FileArchiver"; Flags: uninsdeletevalue; Tasks: FileAssocations
Root: "HKCR"; Subkey: ".tar"; ValueType: string; ValueData: "FileArchiver"; Flags: uninsdeletevalue; Tasks: FileAssocations
Root: "HKCR"; Subkey: ".gz"; ValueType: string; ValueData: "FileArchiver"; Flags: uninsdeletevalue; Tasks: FileAssocations
Root: "HKCR"; Subkey: "FileArchiver"; ValueType: string; ValueData: "FileArchiver"; Flags: uninsdeletekey; Tasks: FileAssocations
Root: "HKCR"; Subkey: "FileArchiver\Shell\Open\Command"; ValueType: string; ValueData: """{app}\FileArchiver.exe"" ""%1"""; Flags: uninsdeletekey; Tasks: FileAssocations
